// multiloader-mc plus common-mc sources. Used by mod loaders

plugins {
    id 'multiloader-mc'
}

configurations {
    commonMcJava{
        canBeResolved = true
    }
    commonMcResources{
        canBeResolved = true
    }
}

dependencies {
    compileOnly(project(':common-mc')) {
        capabilities {
            requireCapability "$group:$mod_id-common-mc"
        }
    }
    commonMcJava project(path: ':common-mc', configuration: 'commonMcJava')
    commonMcResources project(path: ':common-mc', configuration: 'commonMcResources')
}

tasks.named('compileJava', JavaCompile) {
    dependsOn(configurations.commonMcJava)
    source(configurations.commonMcJava)
}

processResources {
    dependsOn(configurations.commonMcResources)
    from(configurations.commonMcResources)
}

tasks.named('javadoc', Javadoc).configure {
    dependsOn(configurations.commonMcJava)
    source(configurations.commonMcJava)
}

tasks.named('sourcesJar', Jar) {
    dependsOn(configurations.commonMcJava)
    from(configurations.commonMcJava)
    dependsOn(configurations.commonMcResources)
    from(configurations.commonMcResources)
}
