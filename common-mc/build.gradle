plugins {
    id 'multiloader-common'
    alias(libs.plugins.neoforge.moddev)
}

dependencies {
    implementation(libs.mclogs.api) {
        exclude group: 'com.google.code.gson', module: 'gson'
    }
    implementation(libs.nightconfig.core)
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

configurations {
    commonMcJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonMcResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonMcJava sourceSets.main.java.sourceDirectories.singleFile
    commonMcResources sourceSets.main.resources.sourceDirectories.singleFile
}
