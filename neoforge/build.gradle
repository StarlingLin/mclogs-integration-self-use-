import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'multiloader-loader'
    alias(libs.plugins.neoforge.moddev)
    alias(libs.plugins.minotaur)
    alias(libs.plugins.curseforge.gradle)
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common-mc').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            gameDirectory = file('runs/client')
            client()
        }
        server {
            gameDirectory = file('runs/server')
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}


dependencies {
    additionalRuntimeClasspath(libs.mclogs.api)
    implementation(libs.mclogs.api)
    jarJar(libs.mclogs.api)
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinth_project_id
    uploadFile = jar
    gameVersions = minecraft_version_list
    loaders = ["neoforge"]
    changelog = changelog
}

tasks.register('curseForge', TaskPublishCurseForge) {
    apiToken = System.getenv('CURSEFORGE_TOKEN')
    projectId = curseforge_project_id_mod
    def mainFile = upload(projectId, jar)
    mainFile.changelog = changelog
    mainFile.changelogType = "markdown"

    for (String version in minecraft_version_list) {
        mainFile.addGameVersion(version)
    }
}
