plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

dependencies {
    additionalRuntimeClasspath(libs.mclogs.api)
    implementation(libs.mclogs.api)
    jarJar(libs.mclogs.api)

    additionalRuntimeClasspath(libs.night.config.toml)
    implementation(libs.night.config.toml)
    jarJar(libs.night.config.toml)
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common-mc').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            gameDirectory = file('runs/client')
            client()
        }
        data {
            gameDirectory = file('runs/data')
            data()
        }
        server {
            gameDirectory = file('runs/server')
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
