plugins {
    id 'multiloader-loader'
    id 'net.neoforged.moddev'
}

dependencies {
    additionalRuntimeClasspath "gs.mclo:api:${mclogs_api_version}"
    jarJar(implementation("gs.mclo:api")) {
        version {
            strictly mclogs_api_version_range
            prefer mclogs_api_version
        }
    }

    additionalRuntimeClasspath "com.electronwill.night-config:toml:${night_config_version}"
    jarJar(implementation("com.electronwill.night-config:toml")) {
        version {
            strictly night_config_version_range
            prefer night_config_version
        }
    }
}

neoForge {
    version = neoforge_version
    // Automatically enable neoforge AccessTransformers if the file exists
    def at = project(':common-mc').file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.add(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
    runs {
        configureEach {
            systemProperty('neoforge.enabledGameTestNamespaces', mod_id)
            ideName = "NeoForge ${it.name.capitalize()} (${project.path})" // Unify the run config names with fabric
        }
        client {
            gameDirectory = file('runs/client')
            client()
        }
        data {
            gameDirectory = file('runs/data')
            data()
        }
        server {
            gameDirectory = file('runs/server')
            server()
        }
    }
    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
